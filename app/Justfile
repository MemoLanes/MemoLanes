set windows-powershell := true

# Show list of available commands
default:
  @echo "ğŸ“‹ Available commands:"
  @just --list

# Build all components
[group: 'dev']
play-build:
    @echo "ğŸ› ï¸  Building all"
    just build-kernel
    just frb-generate
    just build-flutter
    @echo "âœ… Build completed"

# Build Journey Kernel wasm
[group: 'dev']
[working-directory: 'journey_kernel']
build-kernel:
    @echo "ğŸ› ï¸  Building Journey Kernel"
    @yarn build
    @echo "âœ… Journey Kernel build completed"

# Build Flutter
[group: 'dev']
build-flutter:
    @echo "ğŸ› ï¸  Building Flutter"
    @flutter build
    @echo "âœ… Flutter build completed"

# Format code
[group: 'dev']
fmt-code:
    @echo "ğŸ› ï¸  Formatting code"
    @cargo fmt --all
    @echo "âœ… Code formatting completed"

# Generate Flutter Rust Bridge code
[group: 'dev']
frb-generate:
    @echo "ğŸ”— Generating FRB code"
    @flutter_rust_bridge_codegen generate
    @echo "âœ… FRB code generation completed"

# Analyze Flutter code
[group: 'check']
flutter-check:
    @echo "ğŸ” Analyzing Flutter code"
    @flutter analyze --no-fatal-infos
    @echo "âœ… Flutter code analysis completed"

# Check Rust code
[group: 'check']
[working-directory: 'rust']
rust-check:
    @echo "ğŸ” Checking Rust code"
    @cargo check
    @cargo fmt --all -- --check
    @cargo clippy -- -D warnings
    @echo "âœ… Rust code check completed"

# Run Rust tests
[group: 'test']
[working-directory: 'rust']
rust-test:
    @echo "ğŸ§ª Running Rust tests"
    @cargo test -- --nocapture
    @echo "âœ… Rust tests completed"

# Get Flutter Rust Bridge version (private)
[private]
[unix]
get_frb_version:
    @awk '/flutter_rust_bridge:/ { print $2 }' pubspec.yaml