set windows-powershell := true

# Show list of available commands
default:
  @echo "ğŸ“‹ Available commands:"
  @just --list

# ---- Utils ----

[group: 'utils']
[working-directory: 'journey_kernel']
journey-kernel-build:
    @echo "ğŸ› ï¸  Building Journey Kernel"
    @yarn build
    @echo "âœ… Journey Kernel build completed"

[group: 'utils']
frb-generate:
    @echo "ğŸ”— Generating FRB code"
    @flutter_rust_bridge_codegen generate
    @echo "âœ… FRB code generation completed"

[group: 'utils']
[working-directory: 'rust']
rust-fmt:
    @echo "ğŸ› ï¸  Formatting code"
    @cargo fmt --all
    @echo "âœ… Code formatting completed"

[group: 'utils']
[working-directory: 'journey_kernel']
journey-kernel-fmt:
    @echo "ğŸ› ï¸  Formatting code"
    @cargo fmt --all
    @echo "âœ… Code formatting completed"

[group: 'utils']
flutter-fmt:
    @echo "ğŸ› ï¸  Formatting code"
    @dart format .
    @echo "âœ… Code formatting completed"

[group: 'utils']
flutter-check:
    @echo "ğŸ” Analyzing Flutter code"
    @flutter analyze --no-fatal-infos
    @echo "âœ… Flutter code analysis completed"

[group: 'utils']
[working-directory: 'rust']
rust-check:
    @echo "ğŸ” Checking Rust code"
    @cargo check
    @cargo fmt --all -- --check
    @cargo clippy -- -D warnings
    @echo "âœ… Rust code check completed"

[group: 'utils']
[working-directory: 'journey_kernel']
journey-kernel-check:
    @echo "ğŸ” Checking Journey Kernel code"
    @cargo check
    @cargo fmt --all -- --check
    @cargo clippy -- -D warnings
    @echo "âœ… Journey Kernal code check completed"

[group: 'utils']
[working-directory: 'rust']
rust-test:
    @echo "ğŸ§ª Running Rust tests"
    @cargo test -- --nocapture
    @echo "âœ… Rust tests completed"

[group: 'utils']
[working-directory: 'journey_kernel']
journey-kernel-test:
    @echo "ğŸ§ª Running Journey Kernel tests"
    @cargo test -- --nocapture
    @echo "âœ… Journey Kernel tests completed"

[group: 'utils']
[unix]
get-frb-version:
    @awk '/flutter_rust_bridge:/ { print $2 }' pubspec.yaml

# Generate all required files for flutter build. Need to be reran every time rust api or journey kernel is updated
[group: 'dev']
pre-build:
    @echo "ğŸ› ï¸  Running pre-build"
    just journey-kernel-build
    just frb-generate
    @echo "âœ… Pre-build completed"


# Checking all code
[group: 'dev']
check:
    @echo "ğŸ› ï¸  Checking all code"
    just rust-check
    just journey-kernel-check
    just flutter-check
    @echo "âœ… Checking completed"

# Run all tests
[group: 'dev']
test:
    @echo "ğŸ› ï¸  Running all tests"
    just journey-kernel-test
    just rust-test
    @echo "âœ… Test completed"

# Formatting all code
[group: 'dev']
format:
    @echo "ğŸ› ï¸  Running code format"
    just journey-kernel-fmt
    just rust-fmt
    just flutter-fmt
    @echo "âœ… Format completed"

# Release build
[group: 'dev']
release-build:
    @echo "ğŸ› ï¸  Building release version"
    just pre-build
    @flutter build apk --release
    @flutter build ipa --release
    @echo "âœ… Build completed"
